USE [JCARD_SCB]
GO

/****** Object:  StoredProcedure [dbo].[usp_CleanLog]    Script Date: 11/5/2025 12:34:59 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [dbo].[usp_CleanLog]
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @MovedCount INT = 0;

    BEGIN TRY
        BEGIN TRANSACTION;

        ;WITH CTE_Transaction AS (
            SELECT 
               [TXREFID], [TXDT], [TXTIME], [TXCODE], [MTI], [STAN],
               [DTXDT], [DVALUEDT], [TXUPDATEDT], [BANKCODE], [BICCODE],
               [CARDNO], [ACNO], [CARDBIN], [CARDTYPE], [RRN],
               [CATID], [PCODE], [COMCODE], [MERCODE],
               [ISCOMPLETE], [ISVOID], [ISREVR], [AMT], [CCYCODE],
               [STATUS], [ERRCODE], [DESCS], [RESPCD],
               [CHAR01],[CHAR02],[CHAR03],[CHAR04],[CHAR05],[CHAR06],[CHAR07],[CHAR08],[CHAR09],[CHAR10],
               [CHAR11],[CHAR12],[CHAR13],[CHAR14],[CHAR15],[CHAR16],[CHAR17],[CHAR18],[CHAR19],[CHAR20],
               [CHAR21],[CHAR22],[CHAR23],[CHAR24],[CHAR25],[CHAR26],[CHAR27],[CHAR28],[CHAR29],[CHAR30],
               [CHAR31],[CHAR32],[CHAR33],[CHAR34],[CHAR35],[CHAR36],[CHAR37],[CHAR38],[CHAR39],[CHAR40],
               [CHAR41],[CHAR42],[CHAR43],[CHAR44],[CHAR45],[CHAR46],[CHAR47],[CHAR48],[CHAR49],[CHAR50],
               [NUM01],[NUM02],[NUM03],[NUM04],[NUM05],[NUM06],[NUM07],[NUM08],[NUM09],[NUM10],
               [NUM11],[NUM12],[NUM13],[NUM14],[NUM15],[NUM16],[NUM17],[NUM18],[NUM19],[NUM20],
               [DATE01],[DATE02],[DATE03],[DATE04],[DATE05],[DATE06],[DATE07],[DATE08],[DATE09],[DATE10],
               [BILLINFO],[TRANSID],[ISCSHBACK],[UPDATEDT]
            FROM D_EBSTM
            WHERE CAST([DVALUEDT] AS DATE) < CAST(GETDATE() AS DATE)
        )
        DELETE CTE_Transaction
        OUTPUT 
            DELETED.[TXREFID], DELETED.[TXDT], DELETED.[TXTIME], DELETED.[TXCODE], DELETED.[MTI],
            DELETED.[STAN], DELETED.[DTXDT], DELETED.[DVALUEDT], DELETED.[TXUPDATEDT],
            DELETED.[BANKCODE], DELETED.[BICCODE], DELETED.[CARDNO], DELETED.[ACNO], DELETED.[CARDBIN],
            DELETED.[CARDTYPE], DELETED.[RRN], DELETED.[CATID], DELETED.[PCODE], DELETED.[COMCODE],
            DELETED.[MERCODE], DELETED.[ISCOMPLETE], DELETED.[ISVOID], DELETED.[ISREVR], DELETED.[AMT],
            DELETED.[CCYCODE], DELETED.[STATUS], DELETED.[ERRCODE], DELETED.[DESCS], DELETED.[RESPCD],
            DELETED.[CHAR01],DELETED.[CHAR02],DELETED.[CHAR03],DELETED.[CHAR04],DELETED.[CHAR05],DELETED.[CHAR06],DELETED.[CHAR07],DELETED.[CHAR08],DELETED.[CHAR09],DELETED.[CHAR10],
            DELETED.[CHAR11],DELETED.[CHAR12],DELETED.[CHAR13],DELETED.[CHAR14],DELETED.[CHAR15],DELETED.[CHAR16],DELETED.[CHAR17],DELETED.[CHAR18],DELETED.[CHAR19],DELETED.[CHAR20],
            DELETED.[CHAR21],DELETED.[CHAR22],DELETED.[CHAR23],DELETED.[CHAR24],DELETED.[CHAR25],DELETED.[CHAR26],DELETED.[CHAR27],DELETED.[CHAR28],DELETED.[CHAR29],DELETED.[CHAR30],
            DELETED.[CHAR31],DELETED.[CHAR32],DELETED.[CHAR33],DELETED.[CHAR34],DELETED.[CHAR35],DELETED.[CHAR36],DELETED.[CHAR37],DELETED.[CHAR38],DELETED.[CHAR39],DELETED.[CHAR40],
            DELETED.[CHAR41],DELETED.[CHAR42],DELETED.[CHAR43],DELETED.[CHAR44],DELETED.[CHAR45],DELETED.[CHAR46],DELETED.[CHAR47],DELETED.[CHAR48],DELETED.[CHAR49],DELETED.[CHAR50],
            DELETED.[NUM01],DELETED.[NUM02],DELETED.[NUM03],DELETED.[NUM04],DELETED.[NUM05],DELETED.[NUM06],DELETED.[NUM07],DELETED.[NUM08],DELETED.[NUM09],DELETED.[NUM10],
            DELETED.[NUM11],DELETED.[NUM12],DELETED.[NUM13],DELETED.[NUM14],DELETED.[NUM15],DELETED.[NUM16],DELETED.[NUM17],DELETED.[NUM18],DELETED.[NUM19],DELETED.[NUM20],
            DELETED.[DATE01],DELETED.[DATE02],DELETED.[DATE03],DELETED.[DATE04],DELETED.[DATE05],DELETED.[DATE06],DELETED.[DATE07],DELETED.[DATE08],DELETED.[DATE09],DELETED.[DATE10],
            DELETED.[BILLINFO], DELETED.[TRANSID], DELETED.[ISCSHBACK], DELETED.[UPDATEDT]
        INTO R_EBSTM;

        SET @MovedCount = @@ROWCOUNT;

        INSERT INTO D_CLEANLOG
        (
            TXDT, DESCS, RSQUERY, RESULT, STATUS, REQMESS, RESMESS
        )
        VALUES
        (
            GETUTCDATE(),
            'move stm log',
            CAST(@MovedCount AS VARCHAR(50)),
            CONCAT('move D_EBSTM record log : ', @MovedCount, ' lines'),
            'C',
            (
                SELECT 
                    CONVERT(VARCHAR(10), GETDATE(), 120) AS [DATE],
                    'Auto Clean Log' AS [DESC],
                    GETDATE() AS [TXDT],
                    LOWER(CONVERT(VARCHAR(50), NEWID())) AS [uuid]
                FOR JSON PATH, WITHOUT_ARRAY_WRAPPER
            ),
            (
                SELECT 
                    CONVERT(VARCHAR(10), GETDATE(), 120) AS [DATE],
                    'Auto Clean Log' AS [DESC],
                    GETDATE() AS [TXDT],
                    LOWER(CONVERT(VARCHAR(50), NEWID())) AS [uuid]
                FOR JSON PATH, WITHOUT_ARRAY_WRAPPER
            )
        );

        COMMIT TRANSACTION;

        PRINT CONCAT('Moved ', @MovedCount, ' record(s) from D_EBSTM to R_EBSTM');
    END TRY

    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION;

        DECLARE 
            @ErrMsg NVARCHAR(4000),
            @ErrSeverity INT;

        SELECT 
            @ErrMsg = ERROR_MESSAGE(),
            @ErrSeverity = ERROR_SEVERITY();

        RAISERROR(@ErrMsg, @ErrSeverity, 1);
    END CATCH
END
GO


